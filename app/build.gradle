plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id "org.jlleitschuh.gradle.ktlint" version "11.2.0"
}

android {
    namespace 'com.example.met'
    compileSdk 33

    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())

    signingConfigs {
        release {
            storeFile file(properties.getProperty("keyPath"))
            storePassword properties.getProperty("keyPassword")
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyAliasPassword")
        }
    }

    defaultConfig {
        applicationId "com.example.met"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.1'
    }
    packagingOptions {
        resources {
            excludes.add("/META-INF/{AL2.0,LGPL2.1}")
            excludes.add("/META-INF/LICENSE.md")
            excludes.add("/META-INF/LICENSE-notice.md")
        }
    }
}

tasks.getByPath("preBuild").dependsOn("ktlintFormat")

ktlint {
    android = true
    ignoreFailures = false
    disabledRules = ["final-newline", "no-wildcard-imports"]
    reporters {
        reporter "plain"
        reporter "checkstyle"
        reporter "sarif"
    }
}

dependencies {
    implementation project(":feature:feature-search")
    implementation project(":feature:feature-detail")
    implementation project(":core:data")
    implementation project(":core:domain")

    implementation libs.activity.compose
    implementation libs.lifecycle.runtime.ktx

    // hilt
    implementation libs.hilt.android
    kapt libs.hilt.android.compiler
}